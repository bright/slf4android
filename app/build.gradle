plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace "pl.brightinventions.slf4android"
    testNamespace "pl.brightinventions.slf4android.androidTest"

    compileSdk 34

    defaultConfig {
        minSdkVersion 14
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
}

dependencies {
    implementation 'androidx.core:core:1.12.0'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'com.squareup:seismic:1.0.2'
    androidTestImplementation 'org.hamcrest:hamcrest-core:1.3'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation('junit:junit:4.13.2')
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'org.easytesting:fest-util:1.2.5'

    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation('junit:junit:4.13.2')
    testImplementation 'org.easytesting:fest-util:1.2.5'
    testImplementation('org.robolectric:robolectric:4.11.1') {
        exclude module: 'classworlds'
        exclude module: 'commons-logging'
        exclude module: 'httpclient'
        exclude module: 'maven-artifact'
        exclude module: 'maven-artifact-manager'
        exclude module: 'maven-error-diagnostics'
        exclude module: 'maven-model'
        exclude module: 'maven-project'
        exclude module: 'maven-settings'
        exclude module: 'plexus-container-default'
        exclude module: 'plexus-interpolation'
        exclude module: 'plexus-utils'
        exclude module: 'wagon-file'
        exclude module: 'wagon-http-lightweight'
        exclude module: 'wagon-provider-api'
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.bright'
            artifactId = 'slf4android'
            version = '1.0'

            afterEvaluate {
                from components.release
            }
        }
    }
}
